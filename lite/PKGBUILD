# Maintainer: Kyle Keen <keenerd@gmail.com>
pkgname=arch-wiki-lite
pkgver=20200527
_codever=20181022
pkgrel=1
pkgdesc="The wiki without html.  1/9 as big, easily searched and viewable on console."
arch=('any')
url="http://kmkeen.com/arch-wiki-lite/"
_watch="https://www.archlinux.org/packages/community/any/arch-wiki-docs/"
license=('GPL' 'FDL')
depends=('bash' 'gzip')
makedepends=('python' 'arch-wiki-docs')
optdepends=('dialog: for interactive menu'
  'arch-wiki-docs: for wiki-search-html')
source=("http://kmkeen.com/$pkgname/$pkgname-$_codever.tar.gz")
md5sums=('2ddd839ca4aeb9f77248c1e4192c9c75')

build() {
  cd "$srcdir/$pkgname"
  LC_ALL=en_US.UTF-8 python3 wiki_lite.py
}

package() {
  cd "$srcdir/$pkgname"
  install -d "$pkgdir/usr/local/share/doc/arch-wiki/text/"
  install -vm 0644 wiki/* "$pkgdir/usr/local/share/doc/arch-wiki/text/"
  install -d "$pkgdir/usr/local/bin/"
  install -vm 0755 wiki-search      "$pkgdir/usr/local/bin/wiki-search"
  install -vm 0755 wiki-search-html "$pkgdir/usr/local/bin/wiki-search-html"
  install -d "$pkgdir/usr/local/etc/dialog.d"
  install -vm 0644 wiki-search.dialog.rc "$pkgdir/usr/local/etc/dialog.d/wiki-search.dialog.rc"
}
